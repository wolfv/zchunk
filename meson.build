project(
    'zck', 'c',
    version : '1.1.16',
    meson_version : '>=0.44.0',
    default_options : ['c_std=gnu99']
)

so_version = '1'

common_cflags = [
    '-Wunused-result',
]
cc = meson.get_compiler('c')
add_project_arguments(cc.get_supported_arguments(common_cflags), language : 'c')

if build_machine.system() == 'freebsd'
    add_project_arguments('-DFREEBSD', language : 'c')
endif

if get_option('with-zstd') == 'disabled'
    zstd_dep = dependency('', required : false)
else
    if build_machine.system() == 'windows'
        zstd_dep = dependency('zstd', modules: 'zstd::libzstd_shared', required : get_option('with-zstd') == 'enabled')
    else
        zstd_dep = dependency('libzstd', required : get_option('with-zstd') == 'enabled')
    endif
    if zstd_dep.found()
        add_project_arguments('-DZCHUNK_ZSTD', language : 'c')
        if zstd_dep.version().version_compare('<=1.4.9')
            add_project_arguments('-DOLD_ZSTD', language : 'c')
        endif
    endif
endif

if build_machine.system() == 'windows'
    curl_dep = dependency('curl', modules: 'CURL::libcurl')
else
    curl_dep = dependency('libcurl')
endif

inc = []

# if build_machine.system() != 'windows'
message('search path', get_option('prefix') / 'lib')
    if build_machine.system() == 'windows' or build_machine.system() == 'darwin' or build_machine.system() == 'freebsd' or not cc.links('#include <argp.h>\nstatic error_t parse_opt (int key, char *arg, struct argp_state *state) { argp_usage(state); return 0; }; void main() {}')
       inc += include_directories(get_option('prefix') / 'include')
       argplib = cc.find_library('libargp', dirs : get_option('prefix') / 'lib')
    else
       argplib = dependency('', required : false)
    endif
# endif

if get_option('with-openssl') == 'disabled'
    openssl_dep = dependency('', required : false)
else
    if build_machine.system() == 'windows'
        openssl_dep = dependency('openssl', modules : ['OpenSSL::SSL', 'OpenSSL::Crypto'], required : get_option('with-openssl') == 'enabled')
    else
        openssl_dep = dependency('openssl', required : get_option('with-openssl') == 'enabled')
    endif
    if openssl_dep.found()
        add_project_arguments('-DZCHUNK_OPENSSL', language : 'c')
    endif
endif

inc += include_directories('include')
if host_machine.system() == 'windows'
    inc += include_directories('src/lib/win32')
endif

subdir('include')
subdir('src')
if not get_option('coverity')
   subdir('test')
endif

pkg_mod = import('pkgconfig')
pkg_mod.generate(
    libraries : zcklib,
    version : meson.project_version(),
    name : 'libzck',
    filebase : 'zck',
    description : 'A library for generating easy-to-delta files.'
)

install_man([
    'doc/unzck.1',
    'doc/zck.1',
    'doc/zck_delta_size.1',
    'doc/zck_gen_zdict.1',
    'doc/zck_read_header.1',
    'doc/zckdl.1',
])
